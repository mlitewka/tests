apiVersion: templates.gatekeeper.sh/v1beta1
kind: ConstraintTemplate
metadata:
  name: k8spodsecurityrunasrootpolicy
  annotations:
    description: >-
      Non-root user in container should be used
spec:
  crd:
    spec:
      names:
        kind: K8sPodSecurityRunasrootPolicy
  targets:
    - target: admission.k8s.gatekeeper.sh
      rego: |
        package k8spodsecurityrunasrootpolicy

        violation[{"msg": msg}] {
          c := input_containers[_]
          input_run_as_root(c)
          msg := sprintf("Non-root user in container should be used: %v", [c.name])
        }

        input_run_as_root(c) {
          not c.securityContext.runAsNonRoot == true
        }

        input_containers[c] {
            c := input.review.object.spec.containers[_]
        }
        input_containers[c] {
            c := input.review.object.spec.initContainers[_]
        }
        input_containers[c] {
            input.review.object.kind != "Pod"
            input.review.object.kind != "CronJob"
            c := input.review.object.spec.template.spec.containers[_]
        }
        input_containers[c] {
            input.review.object.kind != "Pod"
            input.review.object.kind != "CronJob"
            c := input.review.object.spec.template.spec.initContainers[_]
        }
        input_containers[c] {
            input.review.object.kind == "CronJob"
            c := input.review.object.spec.jobTemplate.spec.template.spec.containers[_]
        }
        input_containers[c] {
            input.review.object.kind == "CronJob"
            c := input.review.object.spec.jobTemplate.spec.template.spec.initContainers[_]
        }